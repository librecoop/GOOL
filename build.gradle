apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'

applicationName = "gool"
mainClassName = 'gool.GOOLCompiler'
sourceCompatibility = 1.7
version = '1.0'
description = 'GOOL is free object-oriented code translation software'

allprojects {
	tasks.withType(JavaCompile) {
		options.debug = true
		options.compilerArgs = ['-Xlint:all']
	}
}

run {
	standardInput = System.in
}

sourceSets{
	main {
		java {
			srcDir 'src/gool'
			srcDir 'src/logger'
			exclude 'test/**'
		}
		resources {
			srcDir 'src'
			exclude 'gool/test'
		}
	}
//	test {
//		java {
//			srcDir 'src/gool/test'
//		}
//	}
}

repositories{
	flatDir {
		dirs 'lib'
		dirs "${System.properties['java.home']}/../lib"
	}
}

dependencies {
	compile name: 'com.ibm.icu_50.1.1.v201304230130'
	compile name: 'commons-lang-2.5'
	compile name: 'hamcrest-all-1.3'
	compile name: 'ini4j-0.5.2-SNAPSHOT'
	compile name: 'junit-4.11'
	compile name: 'log4j-1.2.15'
	compile name: 'org.eclipse.cdt.core_5.5.0.201309180223'
	compile name: 'org.eclipse.core.contenttype_3.4.200.v20130326-1255'
	compile name: 'org.eclipse.core.jobs_3.5.300.v20130429-1813'
	compile name: 'org.eclipse.core.resources_3.8.101.v20130717-0806'
	compile name: 'org.eclipse.core.runtime_3.9.0.v20130326-1255'
	compile name: 'org.eclipse.equinox.common_3.6.200.v20130402-1505'
	compile name: 'org.eclipse.equinox.preferences_3.5.100.v20130422-1538'
	compile name: 'org.eclipse.equinox.registry_3.5.301.v20130717-1549'
	compile name: 'org.eclipse.osgi_3.9.1.v20130814-1242'
	compile name: 'velocity-1.6.4-dep'
	compile name: 'tools'
}



//Application distribution tasks

task copySrc(type:Copy) {
	from('src') {
		include '**/*'
		exclude '**/*.java'
	}
	into "$buildDir/src"
}

task copyTests(type:Copy) {
	from('tests') {
		include 'GOOLINPUTJAVA/**'
		include 'GOOLINPUTCPP/**'
	}
	into "$buildDir/tests"
}

applicationDistribution.from(copySrc) {
	into "src"
}

applicationDistribution.from(copyTests) {
	into "tests"
}

//jar construction tasks

jar{
	manifest{
		attributes 'Main-Class': 'gool.GOOLCompiler',
		'Implementation-Title': 'gool',
		'Implementation-Version': version
	}
}

/*task sourceJar(type: Jar){
	classifier = 'sources'
	from sourceSets.main.allSource
}*/

//Gradle wrapper
task wrapper (type: Wrapper)